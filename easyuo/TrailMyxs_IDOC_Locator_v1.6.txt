;-----------------------------------------------------------
; Script Name: TrailMyx's IDOC Locator
; Author: TrailMyx
; Version: 1.6
; Client Tested with: 5.0.9
; EUO version tested with: 1.5 109
; Shard: OSI, FS
; Revision Date:  3/26/2007
; Public Release: ...
; Global Variables Used: None
; Purpose/Features:
;  1) Scans surrounding area looking for houses in states of decay
;  2) Saves the location, name, time, date and condition to a log file.
;  3) Keeps two lists, one of those houses that are logged from previous searches
;     and another of signs found during the present search.  These are denoted by
;     "New!!!!" and "Logged"
;  4) Pause/Resume function.
;  5) Script will force your character to stop walking if a sign is not loaded.
;     This will make sure you don't miss a sign like you can with other IDOC searching tools
;     out there today.
;  6) Found IDOC houses are colored in the following colors:
;     Blue: Slightly
;     Green: Somewhat
;     Yellow: Fairly
;     Purple: Greatly
;     Red: IDOC
;
; Tips for better use:
;  1) Walking by holding down the R-button on the mouse causes event property to stall
;     during some searchs.  If you walk automatically by first pressing and holding the R-button
;     then holding the L-button, you won't miss any signs, so the script won't force
;     you to stop.
;
;  This script will generate two files:
;  1) SHARD_CHAR_IDOClog.txt holds the log of houses and their info
;  2) SHAR_CHAR_IDOCID.txt holds the FINDIDs of houses found on previous searchs that have been
;     saved with the "Save IDOC" key
;
; Enjoy, TM

gosub AddUnderscore #SHARD
set %shard #RESULT
gosub AddUnderscore #CHARNAME
set %charname #RESULT
set %logfile c:\ , %shard , _ , %charname , _IDOClog , #dot , txt
set %idocidfile c:\ , %shard , _ , %charname , _IDOCID , #dot , txt

gosub Setup
gosub TM_AdvJournalSync SPELLCAST 100
gosub showEUOMenu1

str count %loglist _
set %str #STRRES , #SPC , IDOC(s) , #SPC , loaded!
menu set EUOEdit3 %str

set #MENUBUTTON N/A
set %paused #FALSE

set %lastIDOC N/A

loop1:
  if #MENUBUTTON = EUOButton1
  {
    set #MENUBUTTON N/A
    if %paused = #FALSE
    {
      set %paused #TRUE
      menu delete EUOButton1
      menu Button EUOButton1 236 60 67 25 Continue
    }
    else
    {
      set %paused #FALSE
      menu delete EUOButton1
      menu Button EUOButton1 236 60 67 25 Pause
    }
  }
  
  if #MENUBUTTON = EUOButton2   ; save log list
  {
    set #MENUBUTTON N/A
    display yesno Are you sure you want to overwrite the saved IDOC ID file?
    if #DISPRES = no
      goto loop1
    set %tempidocs %newidocs , %loglist
    execute cmd.exe /c echo set , #SPC , % , loglist , #SPC , %tempidocs > %idocidfile
    menu set EUOEdit3 IDOC list saved!
  }
  
  if #MENUBUTTON = EUOButton3   ; clear log list
  {
    set #MENUBUTTON N/A
    display yesno Are you sure you want to clear your found IDOC list?
    if #DISPRES = no
      goto loop1

    set %loglist
    menu set EUOEdit3 IDOC list cleared!
  }

  if #MENUBUTTON = EUOButton4   ; mark rune
  {
    set #MENUBUTTON N/A
    gosub MarkRune %lastIDOC
  }
  
  if %paused = #TRUE
    goto loop1
  set %conditionflag Refreshed

  finditem %searchlist G_18
  if #FINDKIND <> -1
  {
    finditem #FINDID G
    if #FINDKIND <> -1
    {
      event property #FINDID
      str Count #PROPERTY $
      if #STRRES <= 1
      {
        set %clickx #CLILEFT + #CLIXRES + 20
        set %clicky #CLIYRES + #CLITOP + 20
        click %clickx %clicky dmc r d    ; effectively stops the character.
        goto loop1
      }

      set %lastsign #FINDID
      gosub GetCondition
      
      str pos #PROPERTY $ 1
      set %start #STRRES + 6
      str pos #PROPERTY $ 2
      set %len #STRRES - %start - 1
      str del #PROPERTY 1 %start
      str left #STRRES %len
      set %housename #STRRES

      if %conditionflag <> Refreshed
      {
        set %tempidocs %newidocs , %loglist
        if %lastsign notin %tempidocs
        {
          set %newidocs %newidocs , _ , %lastsign
;          set %loglist %loglist , _ , %lastsign
          for %i 0 6
          {
            sound
            wait 1
          }
          menu get EUOCheckBox1
          if #MENURES = #TRUE
          {
            gosub ConvertDailyTime #TIME
            set %ctime #RESULT
            gosub ConvertDate #DATE
            set %cdate #RESULT
            str left %housename 14
            set %temphousename #STRRES
            execute cmd.exe /c echo Date %cdate Time %ctime , : , #SPC , Shard: , %shard , #SPC , -- , #SPC , Char: , %charname , #spc , -- , #SPC , House: , %temphousename , #SPC , -- , #SPC , Loc:( , #FINDX , #SPC , #FINDY , ) , #SPC , Status: , %conditionflag  >> %logfile
          }
        }
      }

      ignoreitem #FINDID lastsign
    }
  }
  if ( #SCNT2 > ( %refreshtime + 5 ) )
  {
    gosub RefreshScreen
    set %refreshtime #SCNT2
  }
  if ( #SCNT > ( %starttime + 600 ) )
  {
    ignoreitem reset
    set %starttime #SCNT
    goto loop1
  }

  goto loop1
;-----------------------------------------------
sub Setup
  set %page1 QJE_KOE_QNE_OJE_IOE_ONE_UJE_YNE_UNE_SJE_WNE_SNE_GOE_COE_INE_EOE_AOE_GNE_COE_
  set %page2 ILE_OKE_KKE_GLE_MKE_IKE_WKE_SKE_YJE_UKE_QKE_WJE_ALE_GKE_CKE_YKE_EKE_AKE_
  set %page3 IRE_ERE_MLE_GRE_CRE_KLE_MRE_ULE_QLE_KRE_SLE_OLE_ARE_YLE_ELE_YQE_WLE_CLE_
  set %page4 SME_MOE_
  set %searchlist %page1 , %page2 , %page3 , %page4
  set %starttime #SCNT
  set %refreshtime #SCNT2
  set %loglist
  set %newidocs
  call %idocidfile
  set #LPC 100
return
;-----------------------------------------------
sub GetCondition
  event property #FINDID
  str pos #PROPERTY Owner:
  str del #PROPERTY 1 #STRRES
  set %condition #STRRES
  if danger IN #STRRES
  {
    set %conditionflag Collapsing
    menu Font BGColor Red
    menu Font Color White
    return
  }
  if greatly IN #STRRES
  {
    set %conditionflag Greatly
    menu Font BGColor Purple
    menu Font Color White
    return
  }
  if fairly IN #STRRES
  {
    set %conditionflag Fairly
    menu Font BGColor Yellow
    menu Font Color Black
    return
  }
  if somewhat IN #STRRES
  {
    set %conditionflag Somewhat
    menu Font BGColor Green
    menu Font Color White
    return
  }
  if slightly IN #STRRES
  {
    set %conditionflag Slightly
    menu Font BGColor Blue
    menu Font Color White
    return
  }
  else
  {
    set %conditionflag Refreshed
    menu Font BGColor BtnFace
    menu Font Color Black
    return
  }
return
;-----------------------------------------------
sub RefreshScreen
  ignoreitem reset lastsign
  finditem %lastsign G
  if #FINDKIND <> -1
  {
    gosub GetCondition
    gosub GetHouseName #PROPERTY
    set %name #RESULT
    menu set EUOEdit1 %name
    menu set EUOEdit2 %conditionflag
    menu Window Color BtnFace
	  menu delete EUOLabel2
	  menu delete EUOLabel3
	  set %text Coordinates: , #SPC , #FINDX , #SPC , #FINDY
	  menu Text EUOLabel2 4 52 %text
	  set %diffx #FINDX - #CHARPOSX
	  set %diffy #FINDY - #CHARPOSY
    set %text Distance: , #SPC , %diffx , #SPC , %diffy
	  menu Text EUOLabel3 4 68 %text
	  if %conditionflag <> Refreshed
	  {
	    if #FINDID in %loglist
        menu set EUOLabel6 Logged
      if #FINDID in %newidocs
        menu set EUOLabel6 New!!!
      set %lastIDOC #FINDID
    }
    else
    {
      menu set EUOLabel6 ........
    }
  }
  else
  {
    menu Font BGColor BtnFace
    menu Font Color Black
    menu set EUOEdit1 None in range
    menu set EUOEdit2 N/A
	  menu delete EUOLabel2
	  menu delete EUOLabel3
	  menu Text EUOLabel2 4 52 Coordinates: N/A
	  menu Text EUOLabel3 4 68 Distance: N/A
	  menu set EUOLabel6 ........
  }
  ignoreitem %lastsign lastsign
return
;--------------------------------------------------
; %1 - String.
sub GetHouseName
  str pos %1 Name:
  set #STRRES #STRRES + 5
  str del %1 1 #STRRES
  set %name #STRRES
  str pos %name $
  set #STRRES #STRRES - 1
  str left %name #STRRES
return #STRRES
;--------------------------------------------------
; %1 #FINDID of house sign
sub MarkRune
  if %lastIDOC = N/A
    return

  event property %lastIDOC
  gosub GetHouseName #PROPERTY
  set %housename #RESULT
  
  MarkRune_loop1:
    finditem QWL C_ , #BACKPACKID
    if #FINDKIND <> -1
    {
      event property #FINDID
      str count #PROPERTY $
      if #STRRES > 2
      {
        ignoreitem #FINDID runes
        goto MarkRune_loop1
      }
      if #FINDCOL <> 0
      {
        ignoreitem #FINDID runes
        goto MarkRune_loop1
      }

      gosub TM_NewCastSpell 44 #FINDID -1 10 50 ; mark until successful

      set #LOBJECTID #FINDID
      event macro 17 0 ; rename rune
      wait 10
      gosub ConvertDate #DATE
      set %date #RESULT
      gosub ConvertDailyTime #TIME
      set %time #RESULT
      set %housename %housename , #SPC , %date , #SPC , %time
      msg %housename $
    }
    else
    {
      display ok You have no valid runes in your main pack.
    }
return
;--------------------------------------------------
; %1 - string to mung
sub AddUnderscore
  set %tempstring %1
  AddUnderscore_loop1:
    str pos %tempstring #SPC
    if #STRRES <> 0
    {
      set %val #STRRES - 1
      str left %tempstring %val
      set %left #STRRES
      set %val %val + 1
      str del %tempstring 1 %val
      set %tempstring %left , _ , #STRRES
      goto AddUnderscore_loop1
    }
return %tempstring
;-------------------------------------------------------------------------------
; %1 = ConvertDate  changes date to MM/DD/YY format
sub ConvertDate
  str Mid %1 3 2
  set %tempstr #STRRES , /
  str Mid %1 5 2
  set %tempstr %tempstr , #STRRES , /
  str Mid %1 1 2
  set %tempstr %tempstr , #STRRES
return %tempstr

;-------------------------------------------------------------------------------
; %1 = ConvertDailyTime  changes date to HH:MM:SS
sub ConvertDailyTime
  set %ampm AM
  str Mid %1 1 2
  if #STRRES >= 12
  {
    set #STRRES #STRRES - 12
    set %ampm PM
  }
  set %tempstr #STRRES , :
  str Mid %1 3 2
  set %tempstr %tempstr , #STRRES , :
  str Mid %1 5 2
  set %tempstr %tempstr , #STRRES , #spc , %ampm
return %tempstr
;------------------------------------------------------------
sub TM_NewCastSpell
  namespace push
  namespace local NCS
  set !lpc #LPC
  set #LPC 100
  set !whichspell %1
  set !whichtarget %2
  set !castretrymax %3
  set !waitdelay %4
  set !recovery_delay %5

  set !castretry 0
  set !temp_ltargetid #LTARGETID
  set !temp_ltargetkind #LTARGETKIND

  NewCastSpell_loop1:
    if !castretrymax < 0
      goto NewCastSpell_cont1
    if !castretry > !castretrymax
      goto NewCastSpell_end1
    NewCastSpell_cont1:
      gosub TM_AdvJournalSync SPELLCAST
      set #LTARGETKIND 1
      set #LTARGETID !whichtarget
      set !tempmana #MANA
      event macro 15 !whichspell ; cast the spell
      wait !waitdelay
      set !targettimeout #SCNT + 7
      NewCastSpell_wait1:
        gosub TM_AdvJournalScan SPELLCAST VALID spell_fizzles you_have_not_yet mana your_spirit more_reagents
        if #RESULT = #TRUE || #SCNT > !targettimeout
        {
          set !casttimeout #SCNT2 + !recovery_delay
          repeat
          until #SCNT2 > !casttimeout     ; finish up cast delay
          set !castretry !castretry + 1
          goto NewCastSpell_loop1
        }
        if !whichtarget = NONE
          goto NewCastSpell_skip1
        if #TARGCURS = 1
          goto NewCastSpell_targ1
        goto NewCastSpell_wait1 ; wait for target cursor

  NewCastSpell_targ1:
    if !whichtarget = SELF
      event macro 23
    else
      event macro 22

  NewCastSpell_skip1:
    wait 5
    set !casttimeout #SCNT2 + !recovery_delay
    NewCastSpell_skip2:
      if !whichspell >= 0 && !whichspell <= 63 ; Magery
      {
        gosub TM_AdvJournalScan SPELLCAST VALID spell_fizzles there_is_already mana your_spirit more_reagents
      }
      else
      {
        set !cont #FALSE  ; Chivalry, Necromancy, etc
        finditem !whichtarget *
        if !whichtarget in SELF_NONE || #FINDKIND <> -1
          set !cont #TRUE

        if #MANA >= !tempmana && !cont = #TRUE ; check if target is still there
          set #RESULT #TRUE
        else
          set #RESULT #FALSE
      }
      repeat
      until #SCNT2 > !casttimeout     ; finish up cast delay
      if #RESULT = #TRUE
      {
        if !castretrymax > -1
        {
          set !castretry !castretry + 1 ; %castretrymax of -1 will cast until successful
          if !castretry > !castretrymax
            goto NewCastSpell_end1
        }
        goto NewCastSpell_loop1
      }
      if #SCNT2 <= !casttimeout     ; finish up cast delay
        goto NewCastSpell_skip2
  NewCastSpell_end1:
    set #LTARGETID !temp_ltargetid
    set #LTARGETKIND !temp_ltargetkind
    set #LPC !lpc
    namespace pop
return
;-------------------------------------------------------------------------------
sub TM_AdvJournalSync
  namespace push
  namespace local TM_AdvJS_ , %1
  set !_jindex #jindex + 1
  if %0 > 1
    set !lpc_set %2
  namespace pop
  set !TM_FunctionCalled #TRUE
return
;-------------------------------------------------------------------------------
; %1 - Journal Name
; %2 - NONE, ADVANCE , ( _VALID ) - advances jindex pointer, anything else
; %3, %4, %5, etc strings to match
; returns #TRUE for match, #FALSE for no match
;  Will not advance !_jindex pointer to allow for scanning journal history for more than one search.
;  Also searches for : , #SPC in journal entry to be sure someone isn't spamming the text
;  About %2 arguments:
;    NONE: defaults to basic journal scan (no SPAM checking, no #jindex pointer copy advancing)
;    ADVANCE: no spam checking, advances #jindex copy
;    VALID: invokes SPAM filtering, no advance of #jindex copy
;    VALID_ADVANCE, VALIDADVANCE, ADVANCE_VALID, etc.: invokes SPAM filtering, advances of #jindex copy
sub TM_AdvJournalScan
  namespace push
  namespace local TM_AdvJS_ , %1
  set !args %2
  set !temp_lpc #LPC
  if !lpc_set = N/A
    set #LPC 1000
  else
    set #LPC !lpc_set
  set !num_args %0
  set !first_arg 3
  if !_jindex = N/A
    set !_jindex #jindex
  if !charname = N/A
  {
    set !charname #CHARNAME
    AdvJournalScan_loop1:
      str pos !charname #SPC
      if #STRRES <> 0
      {
        set !val #STRRES - 1
        str left !charname !val
        set !left #STRRES
        set !val !val + 1
        str del !charname 1 !val
        set !charname !left , _ , #STRRES
        goto AdvJournalScan_loop1
      }
  }
  set !index !first_arg
  repeat
    set !temp_jindex !_jindex
    set !text % . !index
    while !temp_jindex <= #jindex
    {
      scanjournal !temp_jindex
      str pos #JOURNAL !charname 1
      set !namepos #STRRES
      str count #JOURNAL !charname
      set !namecnt #STRRES
      str pos #JOURNAL :_ 1
      set !smcpos #STRRES
      str pos #JOURNAL !text 1
      set !textpos #STRRES
      if !textpos < !smcpos && !smcpos <> 0 || !smcpos = 1 || :_ notin #JOURNAL || VALID notin !args
        set !pass #TRUE
      else
        set !pass #FALSE
      if ( !text in #journal && ( ( !namepos = 1 && !namecnt <= 1 ) || !pass ) )
      {
        set !temp_jindex !temp_jindex + 1
        if ADVANCE in !args
          set !_jindex !temp_jindex
        set #LPC !temp_lpc
        namespace pop
        set !TM_FunctionCalled #TRUE
        return #TRUE
      }
      set !temp_jindex !temp_jindex + 1
    }
    set !index !index + 1
  until !index - !first_arg > !num_args - !first_arg
  set #LPC !temp_lpc
  namespace pop
  set !TM_FunctionCalled #TRUE
return #FALSE

;--------- EasyUO Menu Designer Code Begin ---------
sub showEUOMenu1
	menu Clear
	menu Window Title TrailMyx's IDOC Locator
	menu Window Color BtnFace
	menu Window Size 310 113
	menu Font Transparent #true
	menu Font Align Right
	menu Font Name MS Sans Serif
	menu Font Size 8
	menu Font Style
	menu Font Color WindowText
	menu Font Transparent #false
	menu Font Align Left
	menu Text EUOLabel1 4 8 Current House
	menu Text EUOLabel2 4 52 Coordinates: N/A
	menu Text EUOLabel3 4 68 Distance: N/A
	menu Text EUOLabel4 24 28 Condition
	menu Text EUOLabel5 4 92 Status:
	menu Text EUOLabel6 140 68
	menu Font BGColor ScrollBar
	menu Edit EUOEdit1 76 4 153 None
	menu Font BGColor BtnFace
	menu Button EUOButton1 236 84 71 25 Pause
	menu Font BGColor ScrollBar
	menu Edit EUOEdit2 76 24 153 None
	menu Font BGColor BtnFace
	menu Check EUOCheckBox1 140 48 85 17 #true Log to file
	menu Button EUOButton2 236 12 71 25 Save IDOC
	menu Button EUOButton3 236 48 71 25 Clear IDOC
	menu Font BGColor ScrollBar
	menu Edit EUOEdit3 44 88 145 None
	menu Font BGColor BtnFace
	menu Button EUOButton4 196 84 35 25 Mark
	menu Show 421 270
return
;--------- EasyUO Menu Designer Code End ---------


